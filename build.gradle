buildscript {
    ext.kotlin_version = '1.0.5-3'
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        maven {
            name = 'sonatype-nexus'
            url = 'https://oss.sonatype.org/content/repositories/public/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: "kotlin"
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'net.minecraftforge.gradle.forge'
    apply plugin: 'org.spongepowered.mixin'

    sourceCompatibility = targetCompatibility = "1.8"

    minecraft {
//        version = "1.10.2-12.18.3.2185"
        version = "1.11-13.19.1.2194"
        runDir = "run"
//        mappings = 'stable_29'
        mappings = "snapshot_20161209"
        makeObfSourceJar = false
    }

    runClient {
        systemProperty "environment", "development"
        systemProperty "mixin.debug", "true"
        systemProperty "mixin.dumpTargetOnFailure", "true"
        systemProperty "mixin.checks", "true"
        jvmArgs = [
                "-javaagent:C:\\Users\\Jesse\\Development\\Tools\\mixin-0.6.1-SNAPSHOT.jar"
        ]
    }

    sourceSets {
        main {
            refMap = "mixins." + project.projectDir.name + ".refmap.json"
            output.resourcesDir = output.classesDir
        }
        main.kotlin.srcDirs = main.java.srcDirs
    }

    def commonManifest = {
        mainAttributes(
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs": "mixins."  + project.projectDir.name +  ".json",
                'ForceLoadAsMod': 'true'
        )
    }
    jar.manifest commonManifest

    // Create a JAR based on deobfuscated code
    task deobfJar(type: Jar) {
        manifest commonManifest
        from sourceSets.main.output
        classifier = 'dev'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'sponge-public'
            url = 'http://repo.spongepowered.org/maven/'
        }
        maven {
            name = 'sonatype-nexus'
            url = 'https://oss.sonatype.org/content/repositories/public/'
        }
        maven { url 'https://jitpack.io' }
        flatDir {
            dirs("$rootProject.projectDir/libs")
        }
    }

    processResources {
        // this will ensure that this task is redone when the versions change.
        inputs.property "version", project.version
        inputs.property "mcversion", project.minecraft.version

        // replace stuff in mcmod.info, nothing else
        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'

            // replace version and mcversion
            expand 'version': project.version, 'mcversion': project.minecraft.version
        }

        // copy everything else, thats not the mcmod.info
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
        }
    }
}

subprojects {
    buildscript {
        ext.kotlin_version = '1.0.5-3'
        repositories {
            mavenCentral()
        }
        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        }
    }

    buildDir = rootProject.buildDir.absolutePath + "/" + project.name
    rootProject.tasks.runClient.classpath += sourceSets.main.output
    rootProject.tasks.runServer.classpath += sourceSets.main.output

    tasks.withType(Jar) {
        destinationDir = file(rootProject.buildDir.absolutePath + "/jars")
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        compile('org.spongepowered:mixin:0.6.1-SNAPSHOT') {
            exclude module: 'launchwrapper'
            exclude module: 'guava'
        }
//        compile 'net.malisis:malisiscore:1.10.2-4.2.10-SNAPSHOT'
        compile 'com.github.sirsavary:MalisisCore:1.11-SNAPSHOT'
        compile 'com.typesafe:config:1.3.1'
        compile 'com.mashape.unirest:unirest-java:1.4.9'
        compile 'org.apache.commons:commons-math3:3.6.1'
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}

dependencies {
    compile project(':mcore'), project(':mclimate'), project(':mfish'), project(':mfarm'), project(':mmetallurgy'), project(':mciv')
}